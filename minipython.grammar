Package minipython;

Helpers
	digit = ['0' .. '9'];
	letter = ['a' .. 'z']|['A' .. 'Z']; 
	cr = 13; /* carriage return (enter) */
	lf = 10; /* line feed '/n' */
	sp = 32;
	all = [0..127]; 
	eol = lf | cr | cr lf ;
	not_eol = [all - [cr + lf]]; 

Tokens
	tab = 9;
	plusplus = '++';
	equalequal = '==';
	minusminus = '--'; 
	mineq = '-=';
	pluseq = '+=';
	diveq = '/=';
	multeq = '*=';
	plus = '+';
	minus = '-';
	pow = '**';
	mult = '*';
	mod = '%';
	div = '/';
	noteq = '!=';
	lesseq = '<=';
	greateq = '>=';
	less = '<';
	great = '>';
	assign = '=';
	def = 'def';
	not = 'not';
	logic_and = 'and';
	logic_or = 'or';
	l_br = '[';
	r_br = ']';
	l_par = '(';
	r_par = ')';
	comma=',';
	in = 'in';
	if = 'if';
	while = 'while';
	for = 'for';
	len = 'len';
	min = 'min';
	max = 'max';
	print = 'print';
	return = 'return';
	assert = 'assert';
	true = 'true';
	semi = ':';
	false = 'false';
	else = 'else';
	none = 'None';
	quote = '"';
	blank = (' ' | lf | cr);
	line_comment = '#' not_eol* eol; 
	number = digit+;
	dot = '.';
	id = letter (letter | digit)*;
	string_double_quotes = '"'not_eol* '"';
	string_single_quotes = '''not_eol* ''';
	everything_else = all;

Ignored Tokens
	blank, line_comment;

Productions
  
	goal = commands *;

	commands = 
				{func} function | 
				{stat} statement  ;

	function = 
				{def_func} tab * def id l_par argument? r_par semi statement ;

	argument = 
				{arg} id assign_value ? ciav*;


	ciav = 
				{comma_id_as_v} comma id assign_value ?;

	assign_value = 
				{as_v} assign value;


	statement =
				{if_statement} tab * if comparison semi statement | 
				{while_statement} tab * while comparison semi statement |
				{for_statement} tab * for [lid]:id in [rid]:id semi statement |
				{return_statement} tab * return expression | 
				{print_statement} tab * print expression comma_expression * |
				{assign_statement} tab * id assign expression |
				{id_mineq_statement}tab * id mineq expression |
				{id_pluseq_statement}tab * id pluseq expression |
				{id_diveq_statement} tab * id diveq expression |
				{array_statement} tab * id l_br [lex]:expression r_br assign [rex]:expression |
				{assertion_statement} tab * assert expression comma_expression? |
				{func_call_statement} tab * function_call ;


	function_call =
				{func_call} id l_par arglist ? r_par ;
	
	arglist = 
				{arglist} expression comma_expression *;


	comma_expression = 
				{comma_expr} comma expression;
	
	comparison =
				{comparison} afteror |
				{comparison_more} comparison logic_or afteror  ;
				
	afteror = 
				{aft_or} afterand |
				{aft_or_more} afteror logic_and afterand ;
			

	afterand = 
				{aft_and} afternot |
				{aft_and_not} not afternot ;

	
	afternot = 
				{true} true|
				{false} false |
				{lesseq} [lpar]:expression lesseq [rpar]:expression |
				{greateq}[lpar]:expression greateq [rpar]:expression |
				{diff} [lpar]: expression noteq [rpar]: expression |   
				{eqeq} [lpar]: expression equalequal [rpar]: expression |  
				{lessc} [lpar]:expression less [rpar]:expression |
				{greatc}[lpar]:expression great [rpar]:expression  ;




	comma_value =
				{cv} comma value;

	expression = 
				{addition_ex} expression plus expressiono | 
				{subtraction_ex} expression minus expressiono|
				{expression_subset} expressiono;

	expressiono = 	
				{simple_expr} simple_expression |
				{pinakas_expr} id l_br expression r_br |
				{func_call_expr} function_call |
				{max_expr} max l_par value comma_value * r_par |
				{min_expr} min l_par value comma_value * r_par ;
				


	simple_expression = 
				{plpl} simple_expression plusplus  |
	 			{minmin} simple_expression minusminus |
				{fac} factor ;
				
				
	factor = 
				{mult_factor} multiplication|
				{ad_factor} plus factor |
				{min_factor} minus factor ;

						
	multiplication = 
				{pow} power | 
				{division} multiplication div power |
				{modulo} multiplication mod power |
				{multiplication} multiplication mult power;


	power = 
				{value} value | 
				{power} power pow value ;
					
							
	value =	
				{len_exp} len l_par expression r_par |
				{id_dot_func} id dot function_call |
				{identifier} ident |
				{number} number |
				{double_quotes} string_double_quotes |
				{single_quotes} string_single_quotes |
				{parenthesis_expr_value} l_par expression r_par |
				{brackets_expr_value} l_br expression comma_expression * r_br|
				{none_value} none ;

	ident = {id} id;